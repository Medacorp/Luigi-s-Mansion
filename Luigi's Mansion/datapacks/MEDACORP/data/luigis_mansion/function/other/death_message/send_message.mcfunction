execute unless data storage luigis_mansion:data my_memory.hurt_by.method.id if score #debug_messages Selected matches 1.. run tellraw @a {"type":"translatable","translate":"luigis_mansion:message.debug.format","color":"white","with":[{"type":"translatable","translate":"luigis_mansion:message.debug","color":"gold"},{"type":"translatable","translate":"luigis_mansion:message.debug.unknown_death_message","color":"red","with":[{"type":"nbt","nbt":"my_memory.hurt_by","source":"storage","storage":"luigis_mansion:data"}]}]}
execute unless data storage luigis_mansion:data my_memory.hurt_by.method.id run data modify storage luigis_mansion:data my_memory.hurt_by.method set value {id:"generic",namespace:"luigis_mansion"}
execute if entity @s[tag=!attacker_exists] run function luigis_mansion:other/death_message/send_message/no_killer with storage luigis_mansion:data my_memory.hurt_by.method
execute if entity @s[tag=attacker_exists] run function luigis_mansion:other/death_message/send_message/one_killer with storage luigis_mansion:data my_memory.hurt_by.method