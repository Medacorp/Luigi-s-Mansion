scoreboard players set @e[tag=this_entity,nbt=!{data:{open_animation:{}}},limit=1] ActionTime -1
scoreboard players set @e[tag=this_entity,nbt={data:{open_animation:{}}},limit=1] ActionTime 0
execute if entity @e[tag=this_entity,nbt={data:{open_animation:{}}},nbt=!{data:{model_data:{open:{}}}},limit=1] if score #debug_messages Selected matches 1.. run tellraw @a {type:"translatable",translate:"luigis_mansion:message.debug.format",with:[{type:"translatable",translate:"luigis_mansion:message.debug",color:"gold"},{type:"translatable",translate:"luigis_mansion:message.debug.missing_open.model_data",color:"red",with:[{type:"selector",selector:"@e[tag=this_entity,limit=1]"}]}]}
execute if entity @e[tag=this_entity,nbt=!{data:{open_animation:{}}},nbt={data:{model_data:{open:{}}}},limit=1] if score #debug_messages Selected matches 1.. run tellraw @a {type:"translatable",translate:"luigis_mansion:message.debug.format",with:[{type:"translatable",translate:"luigis_mansion:message.debug",color:"gold"},{type:"translatable",translate:"luigis_mansion:message.debug.missing_open.animation",color:"red",with:[{type:"selector",selector:"@e[tag=this_entity,limit=1]"}]}]}
execute if data storage luigis_mansion:data furniture{searched:1b} run tag @e[tag=this_entity,limit=1] add open
execute if data storage luigis_mansion:data furniture{searched:1b,no_search_animation:1b} as @e[tag=this_entity,limit=1] run data modify entity @s equipment.head merge from entity @s equipment.head.components."minecraft:custom_data".model_data.open
execute if data storage luigis_mansion:data furniture{no_search_animation:1b} run scoreboard players set @e[tag=this_entity,limit=1] ActionTime -1
execute if data storage luigis_mansion:data furniture{cannot_search_when_open:1b} run tag @e[tag=this_entity,limit=1] add cannot_search_when_open
execute if data storage luigis_mansion:data furniture{cannot_search_when_open:1b,searched:1b} run tag @e[tag=this_entity,limit=1] add drop_loot