$execute unless data storage luigis_mansion:data current_state.current_data{obtained_keys:["$(door)"]} unless entity @e[nbt={ArmorItems:[{components:{"minecraft:custom_data":{entity:{namespace:"luigis_mansion",id:"item"},item:{namespace:"luigis_mansion",id:"key"},variant:"$(door)"}}}]},limit=1] run summon minecraft:armor_stand ~ ~-1.1 ~ {CustomName:'{"type":"translatable","translate":"luigis_mansion:item.key"}',Pose:{Head:[0.0f,0.0f,0.01f]},Invisible:1b,Silent:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_pickaxe",count:1,components:{"minecraft:custom_data":{entity:{namespace:"luigis_mansion",id:"item"},physics:{namespace:"luigis_mansion",id:"generic"},item:{namespace:"luigis_mansion",id:"key"},physics_offset:1.1d,pick_up_range:0.7f,variant:"$(door)",particle:{chance:1,name:"minecraft:crit"},collect_orchestra:1b},"minecraft:unbreakable":{},"minecraft:item_model":"luigis_mansion:entity/heart_key"}}],NoGravity:1b,Tags:["item","no_shrunk","eternal","affected_by_vacuum","same_room_players_only","this_entity"],DisabledSlots:2039583}
execute store result entity @e[tag=this_entity,limit=1] Rotation[0] float 1 run random value -180..179
execute store result entity @e[tag=this_entity,limit=1] Rotation[1] float 1 run random value -90..0
scoreboard players set @e[tag=this_entity,limit=1] EntitySizeRadius 5
scoreboard players set @e[tag=this_entity,limit=1] EntityYOffset 14
execute if entity @e[tag=this_entity,limit=1] unless data storage luigis_mansion:data entity{silent:1b} run playsound luigis_mansion:item.key.spawn player @a[tag=same_room] ~ ~ ~ 1
execute unless entity @e[tag=this_entity,limit=1] if score #debug_messages Selected matches 1.. run tellraw @a {"type":"translatable","translate":"luigis_mansion:message.debug.format","with":[{"type":"translatable","translate":"luigis_mansion:message.debug","color":"gold"},{"type":"translatable","translate":"luigis_mansion:message.debug.loot_spawn_fail.key","color":"red"}]}
execute if entity @e[tag=this_entity,limit=1] run function luigis_mansion:spawn_entities/setup/default
tag @e[tag=this_entity,limit=1] remove this_entity
data remove storage luigis_mansion:data entity