tag @s[tag=!sidestep_movement] add toggle_sidestep_movement
tag @s[tag=toggle_sidestep_movement] add sidestep_movement
tag @s[tag=!toggle_sidestep_movement,tag=sidestep_movement] remove sidestep_movement
tag @s[tag=toggle_sidestep_movement] remove toggle_sidestep_movement

function luigis_mansion:entities/player/memory/get with entity @s

data modify storage luigis_mansion:data menu_options set from storage luigis_mansion:data my_memory.selection_menu.current_options

execute if entity @s[tag=!sidestep_movement] run data modify storage luigis_mansion:data menu_options.options[{components:{"minecraft:custom_data":{option:{id:"show_settings/toggle/movement_type",namespace:"luigis_mansion"}}}}] set value {id:"minecraft:diamond_pickaxe",count:1,components:{"minecraft:item_model":"luigis_mansion:menu/game_boy_horror/show_settings/movement_type_standard","minecraft:unbreakable":{},"minecraft:tooltip_display":{hidden_components:["minecraft:unbreakable"]},"minecraft:attribute_modifiers":[],"minecraft:item_name":{type:"translatable",translate:"luigis_mansion:item.menu.game_boy_horror.show_settings.value",with:[{type:"translatable",translate:"luigis_mansion:item.menu.game_boy_horror.show_settings.movement_type"},{type:"translatable",translate:"luigis_mansion:item.menu.game_boy_horror.show_settings.value.standard"}]},"minecraft:custom_data":{option:{id:"show_settings/toggle/movement_type",namespace:"luigis_mansion"}}}}
execute if entity @s[tag=sidestep_movement] run data modify storage luigis_mansion:data menu_options.options[{components:{"minecraft:custom_data":{option:{id:"show_settings/toggle/movement_type",namespace:"luigis_mansion"}}}}] set value {id:"minecraft:diamond_pickaxe",count:1,components:{"minecraft:item_model":"luigis_mansion:menu/game_boy_horror/show_settings/movement_type_sidestep","minecraft:unbreakable":{},"minecraft:tooltip_display":{hidden_components:["minecraft:unbreakable"]},"minecraft:attribute_modifiers":[],"minecraft:item_name":{type:"translatable",translate:"luigis_mansion:item.menu.game_boy_horror.show_settings.value",with:[{type:"translatable",translate:"luigis_mansion:item.menu.game_boy_horror.show_settings.movement_type"},{type:"translatable",translate:"luigis_mansion:item.menu.game_boy_horror.show_settings.value.sidestep"}]},"minecraft:custom_data":{option:{id:"show_settings/toggle/movement_type",namespace:"luigis_mansion"}}}}

function luigis_mansion:entities/player/selection_menu/load