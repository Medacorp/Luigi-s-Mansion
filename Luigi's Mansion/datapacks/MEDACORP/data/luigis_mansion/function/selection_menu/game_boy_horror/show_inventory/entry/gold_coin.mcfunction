data modify storage luigis_mansion:data macro set value {namespace:"luigis_mansion",id:"gold_coin",uuid:[I;]}
data modify storage luigis_mansion:data macro.uuid set from entity @s UUID
function luigis_mansion:selection_menu/game_boy_horror/show_inventory/check_money with storage luigis_mansion:data macro
execute if score #temp Money matches 0.. run data modify storage luigis_mansion:data menu_options.options append value {id:"minecraft:diamond_pickaxe",count:1,components:{"minecraft:item_model":"luigis_mansion:menu/game_boy_horror/show_inventory/gold_coin","minecraft:unbreakable":{},"minecraft:tooltip_display":{hidden_components:["minecraft:unbreakable"]},"minecraft:attribute_modifiers":[],"minecraft:item_name":{type:"translatable",translate:"luigis_mansion:item.menu.game_boy_horror.show_inventory.entry",with:[{type:"translatable",translate:"luigis_mansion:item.gold_coin"},{type:"text",text:"0"},{type:"text",text:"0"}]},"minecraft:custom_data":{option:{id:"reload"}}}}
execute if score #temp Money matches 0.. in minecraft:overworld run setblock 27 0 0 minecraft:oak_sign{front_text:{messages:[{type:"translatable",translate:"luigis_mansion:item.menu.game_boy_horror.show_inventory.entry",with:[{type:"translatable",translate:"luigis_mansion:item.gold_coin"},{type:"score",score:{objective:"Money",name:"#temp2"}},{type:"score",score:{objective:"Money",name:"#temp"}}]},{type:"text",text:""},{type:"text",text:""},{type:"text",text:""}]}}
execute if score #temp Money matches 0.. in minecraft:overworld run data modify storage luigis_mansion:data menu_options.options[-1].components."minecraft:item_name" set from block 27 0 0 front_text.messages[0]
execute if score #temp Money matches 0.. in minecraft:overworld run setblock 27 0 0 minecraft:bedrock
scoreboard players reset #temp Money
scoreboard players reset #temp2 Money