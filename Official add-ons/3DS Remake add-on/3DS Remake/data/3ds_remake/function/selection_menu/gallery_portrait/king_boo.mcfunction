tag @s add gallery_portrait_menu

data modify storage luigis_mansion:data menu_options set value {options:[]}

execute store result score #temp Time run function 3ds_remake:selection_menu/gallery_portrait/get_portrait_ghost_rank with storage luigis_mansion:data selected_option.portrait_ghost
execute if score #temp Time matches -1 run data modify storage luigis_mansion:data menu_options.options append value {id:"minecraft:diamond_pickaxe",count:1,components:{"minecraft:item_model":"luigis_mansion:menu/dialog/choice/question","minecraft:unbreakable":{},"minecraft:tooltip_display":{hidden_components:["minecraft:unbreakable"]},"minecraft:attribute_modifiers":[],"minecraft:item_name":{type:"translatable",translate:"3ds_remake:item.menu.gallery_portrait.not_caught"},"minecraft:lore":[{color:"white",italic:false,type:"translatable",translate:"3ds_remake:item.menu.gallery_portrait.not_caught.description"}],"minecraft:custom_data":{option:{id:"reload"}}}}
execute if score #temp Time matches 0.. run function 3ds_remake:selection_menu/gallery_portrait/get_portrait_ghost with storage luigis_mansion:data selected_option.portrait_ghost
execute if score #temp Time matches 1.. run data modify storage luigis_mansion:data menu_options.options append value {id:"minecraft:diamond_pickaxe",count:1,components:{"minecraft:item_model":"3ds_remake:menu/gallery_portrait/bronze","minecraft:unbreakable":{},"minecraft:tooltip_display":{hidden_components:["minecraft:unbreakable"]},"minecraft:attribute_modifiers":[],"minecraft:item_name":{type:"translatable",translate:"3ds_remake:item.menu.gallery_portrait.change_frame"},"minecraft:lore":[{color:"white",italic:false,type:"translatable",translate:"3ds_remake:item.menu.gallery_portrait.change_frame.bronze"}],"minecraft:custom_data":{option:{id:"reload",action:"set_king_boo_rank",value:0b}}}}
execute if score #temp Time matches 1.. run data modify storage luigis_mansion:data menu_options.options append value {id:"minecraft:diamond_pickaxe",count:1,components:{"minecraft:item_model":"3ds_remake:menu/gallery_portrait/silver","minecraft:unbreakable":{},"minecraft:tooltip_display":{hidden_components:["minecraft:unbreakable"]},"minecraft:attribute_modifiers":[],"minecraft:item_name":{type:"translatable",translate:"3ds_remake:item.menu.gallery_portrait.change_frame"},"minecraft:lore":[{color:"white",italic:false,type:"translatable",translate:"3ds_remake:item.menu.gallery_portrait.change_frame.silver"}],"minecraft:custom_data":{option:{id:"reload",action:"set_king_boo_rank",value:1b}}}}
execute if score #temp Time matches 2.. run data modify storage luigis_mansion:data menu_options.options append value {id:"minecraft:diamond_pickaxe",count:1,components:{"minecraft:item_model":"3ds_remake:menu/gallery_portrait/gold","minecraft:unbreakable":{},"minecraft:tooltip_display":{hidden_components:["minecraft:unbreakable"]},"minecraft:attribute_modifiers":[],"minecraft:item_name":{type:"translatable",translate:"3ds_remake:item.menu.gallery_portrait.change_frame"},"minecraft:lore":[{color:"white",italic:false,type:"translatable",translate:"3ds_remake:item.menu.gallery_portrait.change_frame.gold"}],"minecraft:custom_data":{option:{id:"reload",action:"set_king_boo_rank",value:2b}}}}
execute if score #temp Time matches 3.. run data modify storage luigis_mansion:data menu_options.options append value {id:"minecraft:diamond_pickaxe",count:1,components:{"minecraft:item_model":"3ds_remake:menu/gallery_portrait/platinum","minecraft:unbreakable":{},"minecraft:tooltip_display":{hidden_components:["minecraft:unbreakable"]},"minecraft:attribute_modifiers":[],"minecraft:item_name":{type:"translatable",translate:"3ds_remake:item.menu.gallery_portrait.change_frame"},"minecraft:lore":[{color:"white",italic:false,type:"translatable",translate:"3ds_remake:item.menu.gallery_portrait.change_frame.platinum"}],"minecraft:custom_data":{option:{id:"reload",action:"set_king_boo_rank",value:3b}}}}
execute if score #temp Time matches 0.. unless data storage luigis_mansion:data selected_option.no_portrait_battles run function 3ds_remake:selection_menu/gallery_portrait/get_portrait_battle_available with storage luigis_mansion:data selected_option.portrait_ghost
scoreboard players reset #temp Time
data modify storage luigis_mansion:data menu_options.options[].components."minecraft:custom_data".option.uuid set from storage luigis_mansion:data selected_option.uuid
data modify storage luigis_mansion:data menu_options.options[].components."minecraft:custom_data".option.portrait_ghost set from storage luigis_mansion:data selected_option.portrait_ghost
data modify storage luigis_mansion:data menu_options.options[].components."minecraft:custom_data".option.portrait_battle_position set from storage luigis_mansion:data selected_option.portrait_battle_position
data modify storage luigis_mansion:data menu_options.options[].components."minecraft:custom_data".option.portraits set from storage luigis_mansion:data selected_option.portraits
data modify storage luigis_mansion:data menu_options.options[].components."minecraft:custom_data".option.back_overwrite set value {id:"king_boo"}

function luigis_mansion:entities/player/selection_menu/load